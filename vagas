#!/usr/bin/env perl

use Mojolicious::Lite;
use Mojo::IOLoop;
use Mango;

BEGIN {
  use File::Basename 'dirname';
  push @INC, dirname(__FILE__);
}

use ParseVagas;

# Documentation browser under "/perldoc"
plugin 'PODRenderer' if app->mode eq 'development';

get '/' => sub {
  my $self = shift;

  $self->redirect_to('index.html');  

};

get '/inicio' => sub {
  my $self = shift;
  
  my $url = $self->url('/cgi/pesqvaga4.cfm');

  $self->render_later;

  Mojo::IOLoop->timer(0 => sub {
    my $tx = $self->obter_ua->get($url);
    $self->render(json => { sessao => $self->stash('session_id') });
  });

};

get '/captcha' => sub {
  my $self = shift;

  my $url = $self->url('/cgi/kaptcha.cfm');

  $self->render_later;
  
  Mojo::IOLoop->timer(0 => sub {
    my $tx = $self->obter_ua->get($url);
    # TODO melhorar o tratamento aqui
    $self->res->headers->content_length($tx->res->content->body_size);
    $self->finish($tx->res->content->get_body_chunk(0));
  });  
};

post '/pesquisar' => sub {
  my $self = shift;
  my $dados = $self->req->json;

  my $url = $self->url('/cgi/pesqvaga4.cfm');

  $self->render_later;

  Mojo::IOLoop->timer(0 => sub {
    my $tx = $self->obter_ua->post($url => 
      form => {pag => '1',
         tv => '1010',
         estado => 'SP',
         codvaga => '',
         ddmmaa1 => '',
         ddmmaa2 => '',
         andor => '1',
         keyw => $dados->{filtro},
         code => $dados->{verificacao},
         submit => 'ok'
      }
    );
    
    # salvar cada resposta no mongolab
    my $vagas = ParseVagas::fazer($tx->res->body);
    $self->render(json => $vagas);
    Mojo::IOLoop->timer(0 => sub { salvar_cache($self, $vagas); });

  }); 
};

post '/paginar' => sub {
  my $self = shift;
  my $dados = $self->req->json;

  my $url = $self->url('/cgi/list4.cfm');

  $self->render_later;

  Mojo::IOLoop->timer(0 => sub {
    my $tx = $self->obter_ua->post($url => 
      form => {pag => $dados->{pag},
        tv => $dados->{tv},
        estado => 'SP',
        codvaga => '',
        ddmmaa1 => '',
        ddmmaa2 => '',
        andor => '1',
        keyw => $dados->{filtro},
        cargoperfil => '0'
      }
    );
  
    # salvar cada resposta no mongolab
    my $vagas = ParseVagas::fazer($tx->res->body);
    $self->render(json => $vagas);  
    Mojo::IOLoop->timer(0 => sub { salvar_cache($self, $vagas); });
  }); 
};

get '/cache/:vagaId' => [ vagaId => qr/\d+/ ] => sub {
  my $self = shift;

  $self->render_later;

  # recuperar resposta no mongolab
  Mojo::IOLoop->timer(0 => sub {
    my $mango = Mango->new($self->config->{servidor}->{mongodb});
    my $doc = $mango->db('vagas')->collection('vagas')->
      find_one({ codigo => $self->param('vagaId') });

    if ($doc) {
      delete $doc->{_id};
      $self->render(json => {tv => 1,
        op => [$doc]});   
    } else {
      $self->render(json => {tv => 0,
        erro => 'CÃ³digo nÃ£o encontrado: ' . $self->param('vagaId'),
        op => []});        
    }
  });


};

sub salvar_cache {
  my $self = shift;
  my $dados = shift;
  my $mango = Mango->new($self->config->{servidor}->{mongodb});
  my $vagas_collection = $mango->db('vagas')->collection('vagas');
  my $vagas_ids_collection = $mango->db('vagas')->collection('vagas_ids');
  for my $vaga (@{$dados->{op}}) {
    # nao esta funcionando a versao nao bloqueante
    my $doc = $vagas_ids_collection->find_one({ codigo => $vaga->{codigo} });
    unless ($doc) {
      $vagas_collection->insert($vaga);
      $vagas_ids_collection->insert({ codigo => $vaga->{codigo} });
    } 
  }
}

plugin Config => {file => 'vagas.conf'};
plugin 'VagasHelpers';

app->secrets('xxxxxxxxxx');
app->start;

